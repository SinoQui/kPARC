tok:{" "\'0:x};lst:{(. :/)'tok x};col:{(tok y)[;x]}

(+/x),1+*&0>+\x:1 -1"()"?*0:`p1

(+/{+/7#x*1_x,*x}'x),+/{(*/x)+2*+/2#x}'x:{x@<x:.:'"x"\x}'0:`p2

(#?0,+\x),#?0,,/++\0N 2#x:(x,-x:1,#x)x:"^>v<"?*0:`p3

/md5:{6?"0123456789abcdef"};(~&/"0"=5#md5(*0:`p4),$:)(1+)/1

(+/{(&/~($`ab`cd`pq`xy)in 2':x)&(|/=':x)&2<+/x in"aeiou"}'x),+/{(|/x in'(2+!#x)_\:x:2':x)&|/(-2_x)=2_x}'x:0:`p5

x:{{x+!:'1+y-x}/.:'(|x)2 0}'tok`p6;f:"fn "?(0:`p6)@'6;(+/+/./[1000 1000#0;x;(0;1;~:)f]),+/+/./[1000 1000#0;x;(0|-1+;1+;2+)f]

x:(:/'x)!x:tok`p7;A:&;O:|;L:{y_x,&y:2/y};R:{|L[|x]y};e:{$["a">*x;(16#2)\. x;^@a:d x;d[x]:$[3=#a;e@*a;4=#a;~e a 1;(.*a 1)/e'a 0 2];a]};d:x;a:2/r:e@,"a";d:x;d[,"b"]:r;a,2/e@,"a"

(+/{2++/(1+2*"x"=1_x)*<\"\\"=-1_x}'x),+/{2++/x in"\"\\"}'x:0:`p8

(&/x),|/x:{(+/1_x':)'cmb 8}x|+x:@[;!8]'(0,+\!7)_|lst`p9

f:{,/+(-':1_i,#x;x i:&~=':x)};x:-48+*0:`p10;#40 f/x /;#50 f/x

/\t a:3#x:-97+*0:`p11;f:|/(~|/1_&':1=1_-':;|/8 11 14 in;2>+/<\=':)@\:;n:26\1+26/;`c$97+a,x:f n/3_x /;`c$97+a,f n/n x

f:{1_'(&(x in"{[,")&1=+\(x in"{[")-x in"}]")_-1_x}
k:{$["["=*x;o'f x;"{"=*x;!/+{(. n#x;k(1+n:x?":")_x)}'f x;. x]}

(+/"i"$1_'(&x in",:[]}")_x),{$[x~*x;x;$[`.=@x;|/"red"~/:x;`C=@x];0;+/o'x]}k x:*0:`p12

m:(8^'!8)@[&8;;]'8 7#{(-1 1"g"=*x)*. y}/'col[2 3]`p13;(|/{+/1_m':x,|x,:*x}'x),|/{+/1_m':x,|x}'x:cmb 8

(|/+/x),|/+/a=|/'a:+\x:+{2503#(*x)*~&1_x}'.:''col[3 6 13]`p14

d:{$[x=1;,,y;,/n,/:'(x-1)o'y-n:!1+y]};(|/*/-1_x),|/*/-1_x@\:&500=(x:0|+d[4;100](+/*)\:{. x^","}''q:col[2+2*!5]`p15)4

f:(!d)!(=;<;=;>;=;=;>;<;=;=)@'. d:("children";"cats";"samoyeds";"pomeranians";"akitas";"vizslas";"goldfish";"trees";"cars";"perfumes")!3 7 2 3 0 0 5 3 2 1
(1+*&(&/d=)'x),1+*&(&/f@')'x:(.:'!/+3 2#)'^[;":,"]''col[2+!6]`p16

(#x),+/n=&/n:+/s@\:x:&150=+/x*s:!2+&#x:lst`p17

f:{3=s-x&4=s:2(+-1+/':)/x};(+/+/100 f/x),+/+/100(m|f@)/(m:m*\:m,:|m:~!50)|x:"#"=0:`p18

m:**|r:tok`p19;r:-2_r[;2 0];(#?,/{?[m;;x]'m$y}/'r),-1+#?(1<#:){?[x].(*x$;)@'*1?r}\m

d:{?f,_x%f@:&~1!x%f:1+!_%x};N:.*0:`p20;({N>+/10*d x}(M+)/M),{N>+/11*(x<51*)#d x}(M+)/M:420

B:lst`p21;b:{</-_-(100,*B)%1|(B 1;4+x+1 2 3$3#S z)-(y+1 2 3$3_S z;B 2)}.;g:+/(8 10 25 40 74;0 13 31 53 75 102;25 50 100 20 40 80$/:S:(3>+/)#+!6#2)@';(&/g'b_x),|/g'b#x:+!5 6,#S

B:lst`p22;C:113 173 229 53 73;T:6 6 5 1 1;A:0 0 2 0,,0 1;W:0 3 -101 4,,2 -2;s:{x[A i]-:W i:&0<4_x;x[4+i]-:1;x}
n:{{0<x 1}#x{x[2 3,4+y]+:(-1 1*C y),T y;x[1]-:(0<*x)*B[1]-7*0<(x:s x)4;x}/:&~(C>x 2)|0<4_x:s x}
f:{$[~#y;0N;#w:&1>*:'v:?y@&b:m<53+&/m:y@'3;&/v[w;3];o[x]y[&~b],,/n'@[;1;x+]'v]}
(f[0]x),f[-1]x:,(*B),50 500 0,&#C

a:0;b:1;I:{2_x^"+"}'0:`p23;p:{x};o:{$[1=*R;x 1;1]};e:{$[2!*R;1;x 1]};c:{R[x]+:1;1};l:{R[x]*:3;1};f:{R[x]%:2;1};{R::x,0;((#I)>){x+. I x}/0;R 1}'0 1

s:+/x:lst`p24;&/*/'((s%4)=+/)#cmb[4]x /;&/*/'((s%3)=+/)#cmb[6]x

x@:&~^x:"i"$-1_'*tok`p25;_(100000&(+/!+/x)-*x)(33554393!252533*)/20151125
\\
